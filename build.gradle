plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'info.repy'
version System.getProperty("build.sourcebranch") == null ? "1.0.0" : System.getProperty("build.sourcebranch").toString().split("/")[2]

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

java{
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('software.amazon.awssdk:bom:2.5.+')
    implementation 'software.amazon.awssdk:dynamodb'
    testImplementation 'junit:junit:4.12'
    def lombokVersion = "1.18.8"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'DynamoDB Converter'
                description = 'DynamoDB Converter. Java POJO and JSON supported.'
                url = 'https://github.com/Repy/dynamodb-converter'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                developers {
                    developer {
                        id = 'repy'
                        name = 'Repy'
                        email = 'gamecube02+git@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Repy/dynamodb-converter.git'
                    developerConnection = 'scm:git:https://github.com/Repy/dynamodb-converter.git'
                    url = 'https://github.com/Repy/dynamodb-converter.git'
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://maven.pkg.github.com/Repy'
            credentials {
                username "repy"
                password System.getProperty("github.repy.token")
            }
        }
    }
}

